# Comando de Inicio: empezar
para empezar
  repite 99 [nuevo retrocede 160 bajalapiz arbol 80 espera 200]
fin

para nuevo
# Inicializamos pantalla, papel, tortuga, ...
  reponetodo ptp [400 400]
  poncolorpapel negro poncolorlapiz verde subelapiz ocultatortuga
fin

para arbol :long
  si :long< 10 [yema alto]         # Fin de recursion si long muy corta
  pongrosor :long/9       # Reduce grosor lapiz al menguar la rama
  poncolorlapiz arbolcol :long        # color de rama depende de long
  hazlocal "angulo elige [24 32 40 48 56 64 72]
  hazlocal "arbolangulo :angulo*elige [0.25 0.375 0.5 0.625 0.75]
  avanza :long
  giraizquierda :arbolangulo
  arbol :long * elige [0.7 0.75 0.8] giraderecha :angulo
  arbol :long * elige [0.7 0.75 0.8] giraizquierda :angulo
  giraderecha :arbolangulo
  subelapiz retrocede :long bajalapiz        # Volvemos a punto de partida
fin

para yema
  haz "micolorlapiz colorlapiz haz "mirumbo rumbo
  poncolorlapiz 6 ponrumbo 180
  avanza 5 retrocede 5 poncolorlapiz :micolorlapiz ponrumbo :mirumbo
fin

para arbolcol :long
  haz "green redondea 2.5*(100-:long)        # color depende de long
  devuelve ( lista 255 :green 0 )        # rojo si vale 0 y amarillo para 255
fin
