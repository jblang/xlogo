# Comando de Inicio: empezar
para empezar
  nuevo leyenda inicializar
  repitepara [m 1 7] [
  haz "mod elemento :m :modos
  ponposicion lista ((54*:m) - 230) cambiasigno 170
  repitepara [n 1 11] [
  haz "pcolor frase elemento :m :modos elemento :n :collist
  poncolorlapiz ejecuta frase :pcolor []
  tipea :pcolor escribe lista "\ = colorlapiz
  paleta :pcolor
  ] escribe [] ]
fin

para nuevo
# Inicializamos papel, lapiz y tortuga
  reponetodo ptp [400 400]
  poncolorpapel negro subelapiz ocultatortuga
fin

para inicializar
  pongrosor 12
  haz "collist [Negro Rojo Verde Amarillo Azul Magenta Cyan Blanco Gris       Naranja Violeta]
  haz "modos [ [] claro oscuro [claro claro] [oscuro oscuro] [claro oscuro]       [oscuro claro]]
fin

para paleta :col
  giraderecha 90 bajalapiz avanza 16 subelapiz
  retrocede 16 giraizquierda 90 avanza 30
fin

para leyenda
# Muestra encabezado y pie
  poncolorlapiz blanco subelapiz ponposicion [-150 170]
  hazlocal "leyenda [color claro oscuro claroclaro
     oscurooscuro clarooscuro oscuroclaro ]
  hazlocal "subebaja (-1)
  repite 7
   [ ponxy (54 *contador - 230) (170 + :subebaja * 10)
     giraderecha 90
     retrocede 0.5*largoetiqueta elemento contador :leyenda
     giraizquierda 90
     rotula elemento contador :leyenda
     haz "subebaja (-:subebaja) ]
fin

para claro :col
  repitepara [c 1 3] [
  haz "color elemento :c :col
  haz "color :color + 0.5 * (255 - :color)
  haz "col reemplaza :col :c :color]
  devuelve :col
fin

para oscuro :col
  repitepara [c 1 3] [
  haz "color elemento :c :col
  haz "color :color - 0.5 * (:color)
  haz "col reemplaza :col :c :color]
  devuelve :col
fin
