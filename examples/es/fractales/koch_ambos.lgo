# Comando de Inicio: empezar 6
para empezar :nivel
  nuevo
  koch.int :nivel 320
fin

para koch.ext :nivel :lado
  ponrumbo 30
  repite 3
    [koch :nivel :lado giraderecha 120]
fin

para koch :nivel :lado
  si :nivel < 1 [
  avanza :lado alto]
  koch :nivel-1 :lado/3 giraizquierda 60
  koch :nivel-1 :lado/3 giraderecha 120
  koch :nivel-1 :lado/3 giraizquierda 60
  koch :nivel-1 :lado/3
fin

para nuevo
# Inicializamos papel, lapiz y tortuga
  reponetodo ptp [400 400]
  poncolorpapel negro subelapiz ocultatortuga
  poncolorlapiz 7 ponposicion [-160 -90] bajalapiz
fin

para koch.int :nivel :lado
  ponrumbo 90
  repite 3
    [ koch :nivel :lado giraizquierda 120]
fin

para ambos :nivel.ext :nivel.int
  nuevo poncolorlapiz rojo
  koch.ext :nivel.ext 320
  poncolorlapiz azul
  koch.int :nivel.int 320
fin
