# Main Command: Go 6
# Chirral Star by Guy Walker
# www.cr31.co.uk/logoarts

to new
  # set default screen, pen and turtle 
  ResetAll SetScreenSize [400 400] HideTurtle 
  SetSC Black SetPC Green SetPS 1 PenUp 
end

to chiral :order :side
  If :Order < 0 [Stop] 
  Back :Side/2 Left 90 Forward :Side/2 Right 90
  # SetPC (List :Side :Side :Side)     # comment in for shaded squares
  Square :Side
  Forward :Side Chiral :Order-1 :Side/2
  Right 90 Forward :Side Chiral :Order-1 :Side/2
  Right 90 Forward :Side Chiral :Order-1 :Side/2
  Right 90 Forward :Side/2 Right 90 Forward :Side/2
end

to go :order
  New Make "Side 384 
  Square :Side
  SetPC Black Chiral :Order :Side/2 
end

to square :side
  # square drawn from centre
  Make "Side :Side-1
  Make "Half :Side/2
  Back :Half Right 90 Back :Half PenDown
  Repeat 4 [Forward :Side Left 90] PenUp
  Forward :Half Left 90 Forward :Half
  Fill Wait Integer :Side/6
end
