# Main Command: GoX
# Wirth Curve by Guy Walker
# www.cr31.co.uk/logoarts

to new
  # set default screen, pen and turtle 
  ResetAll SetScreenSize [400 400] HideTurtle 
  SetSC Black SetPC Green SetPS 1 PenUp 
end

to init :order
  Make "Side (3*Power 2 :Order) - 1     # centre curve 
  Make "Size 380/:Side     # make Size a global value
end

to wi :order :angle
  If :Order < 1 [Forward :Size Stop]     # ie if = 0
  Right :Angle
  Iw :Order Minus :Angle
  Left :Angle Forward :Size Left :Angle
  Iw :Order Minus :Angle
  Right :Angle
end

to iw :order :angle
  Right :Angle
  Wi :Order-1 Minus :Angle
  Forward :Size/2 Left 2*:Angle Forward :Size/2 
  Wi :Order-1 Minus :Angle
  Right :Angle
end

to go :order
  New Init :Order
  SetPos List Minus 190 (Minus 190)+:Size/2
  SetPC Yellow PenDown
  Repeat 4 [Wi :Order 45 Forward :Size/2 Right 90 Forward :Size/2]
  PenUp Home SetPC DarkBlue Pendown Fill     # comm out for no fills
end

to gox
  New Animation 
  For [Order 0 5] [Wash     # comment out Wash for overlaid curves
    Init :Order
    SetPos List Minus 190 (Minus 190)+:Size/2
    SetPC Yellow PenDown
    Repeat 4 [Wi :Order 45 Forward :Size/2 Right 90 Forward :Size/2]
    PenUp Home SetPC DarkBlue Pendown Fill     # comm out for no fills
    Refresh Wait 80 PenUp Home]
end
