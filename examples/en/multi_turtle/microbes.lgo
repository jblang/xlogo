# Main Command: Go
# Microbes by Guy Walker
# www.cr31.co.uk/logoarts

to new
  # set default screen, pen and turtle
  ResetAll SetScreenSize [400 400] HideTurtle
  SetSC Black SetPC Green SetPS 1 PenUp
end

to roam
  PenUp Forward 3 Left (6-Random 12) EdgeTest
end

to oc
  SetPC White PenDown SetPenWidth 42
  Forward 36 Back 72 Forward 36
end

to ic
  SetPC Black PenDown SetPenWidth 36
  Forward 36 Back 72 Forward 36
end

to edgetest
  If (First Pos) > 260 [SetX -260 Stop]
  If (First Pos) < -260 [SetX 260 Stop]
  If (Last Pos) > 260 [SetY -260 Stop]
  If (Last Pos) < -260 [SetY 260 Stop]
end

to go
  New Animation SetTurtlesMax 32
  SetPC White
  For [N 0 31] [SetTurtle :N PenUp
    SetH 11.25*:N]
  Forever [
    For [N 0 31] [SetTurtle :N Roam OC]
    For [N 0 31] [SetTurtle :N IC]
    Refresh Wait 5 Wash]
end
