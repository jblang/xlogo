# Main Command: Go
# Intertwine by Guy Walker
# www.cr31.co.uk/logoarts

to new
  # set default screen, pen and turtle 
  ResetAll SetScreenSize [400 400] HideTurtle 
  SetSC Black SetPC Green SetPS 1 PenUp 
end

to ring :rep
  Make "Parity Minus :Parity
  Repeat :Rep [
    Make "PenC Abs :PenC -7 SetPC :PenC
    Forward 3*:Rep 
    Left :Parity *18 Square 13 Right :Parity *18 
    Back 3*:Rep Left 360/:Rep]
end

to square :side
  # square (side x side) drawn from centre (tx)
  LocalMake "Half :Side/2 
  PenUp Back :Half Right 90 Back :Half
  PenDown Repeat 4 [ 
    Forward :Side Left 90] 
  PenUp Forward :Half Left 90 Forward :Half 
end

to go
  New
  SetSC LightGray SetPC White 
  Make "Parity 1 Make "PenC 7
  ForEach "Rep [60 46 32 20] [Ring :Rep]
end
