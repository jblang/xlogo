# Main Command: Go
# Flowers by Guy Walker
# www.cr31.co.uk/logoarts

to new
  # set default screen, pen and turtle 
  ResetAll SetScreenSize [400 400] HideTurtle 
  SetSC Black SetPC Green SetPS 1 PenUp 
end

to arch
  # draw single 90 degree arc 
  Repeat 18 [Forward 4 Right 5]
end

to oval
  # draw single oval shape (tp)
  Left 45 Arch Right 90
  Arch Right 135 Wait 6
end

to base
  # draw single base (tp)
  Right 90 Back 30 PenDown
  SetPC LightGray Oval
  PenUp Forward 30 Left 90
end

to leaf
  # draw single leaf (tp) 
  SetPC Green
  Forward 24 Oval Back 24
end

to section
  # draw short stem and 2 leaves
  Right 45 Leaf Left 45 Forward 24 
  Left 45 Leaf Right 45 Forward 24 
end

to stem
  # draw 3 sections
  SetPC Green Forward 48
  Repeat 3 [Section]
  PenUp Forward 64 PenDown
end

to petal
  # draw single petal (tp)
  SetPC Red Oval
end

to flower
  # draw flower head (tp)
  Repeat 18 [
  SetPC Yellow Forward 4
  Petal
  PenUp Back 4 Right 20 PenDown]
end

to plant
  # draw flower (tp)
  Stem Flower
  PenUp Back 256 PenDown
end

to bunch
  # draw 3 flowers
  Left 24 PenDown
  Repeat 3 [Plant Right 24]
end

to go
  # CS and go to start point 
  New Back 150
  Base Bunch
end
