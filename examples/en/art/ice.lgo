# Main Command: Go
# Ice by Guy Walker
# www.logoarts.co.uk

to new
  # set default screen, pen and turtle 
  ResetAll SetScreenSize [400 400] HideTurtle 
  SetSC Black SetPC Green SetPS 1 PenUp 
end

to drawtile
  SetPos [-190 -190]
  PenDown Repeat 4 [Forward 380 Right 90] PenUp
  Home Fill
end

to crack
  Make "Point RandomPoint SetPos :Point
  If 0=(Item 3 FindColor Pos) [Make "OnLine :OnLine+1 Stop]
  SetH Random 360
  While [255=(Item 3 FindColor Pos)] [Back 1]
  Make "BackPos Pos SetPos :Point
  While [255=(Item 3 FindColor Pos)] [Forward 1]
  PenDown SetPos :BackPos PenUp
end

to randompoint
  Output List (Random 360)-180 (Random 360)-180
end

to colorize
  Repeat 48 [
  SetPos RandomPoint
  If 255=(Item 3 FindColor Pos) [
    SetPC ( List 0 Random (Integer 127 + Random 100) 255 )
    Fill ] ]
end

to go
  New SetPC [102 153 255] DrawTile
  Make "OnLine 0 SetPC Black SetPW 2    # SetPW 1 for thin ice
  While [:OnLine < 36] [Crack]
  Colorize
end
