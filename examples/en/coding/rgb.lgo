# Main Command: Go
# RGB Colors by Guy Walker
# www.cr31.co.uk/logoarts

to new
  # set default screen, pen and turtle
  ResetAll SetScreenSize [400 400] HideTurtle
  SetSC Black SetPC Green SetPS 1 PenUp
end

to rgb
  Make "Red [Abs 255 *Sin (:X/2)]
  Make "Gre [Abs 255 *Sin (:X/2 + 120)]
  Make "Blu [Abs 255 *Sin (:X/2 + 240)]
end

to disp
  SetPC White SetPos [-180 150]
  Label [RGB Color Palette]
  SetPos [-180 147]
  Right 90 PenDown Forward 100 PenUp Left 90
  SetPos [-170 100] SetPC Red
  Label Sentence "Red= :Red
  SetPos [-170 80] SetPC Green
  Label Sentence "Grn= :Gre
  SetPos [-170 60] SetPC Blue
  Label Sentence "Blu= :Blu
end

to axiss
  SetPC White SetPos [-180 0] PenDown
  SetPos [-180 -86] SetPos [181 -86] PenUp
  SetPos [-190 8] Label "255
  SetPos [-192 -88] Label "0
  SetPos [184 -88] Label "X
  SetPos [-182 -104]
  For [A 0 360 60] [Label :A Right 90 Forward 58 Left 90]
end

to graph
  For [X 0 360] [
    Make "R Run :Red
    Make "G Run :Gre
    Make "B Run :Blu
    SetPC ( List :R 0 0 ) Dot List :X-180 :R/3-85
    SetPC ( List 0 :G 0 ) Dot List :X-180 :G/3-85
    SetPC ( List 0 0 :B ) Dot List :X-180 :B/3-85
    SetPC ( List :R :G :B ) Spectrum :X]
end

to spectrum :x
  SetXY :X-180 Minus 130 PenDown SetY -160 PenUp
  SetXY 140 150 SetHeading :X SetPW 2 PenDown 
  Forward 30 PenUp SetPW 1
end

to go
  New RGB Disp Axiss Graph
end

to rgb1
  # this is library item 'Hue' 
  Make "Red [127.5 *(1+Sin :X)]
  Make "Gre [127.5 *(1+Sin (:X + 120))]
  Make "Blu [127.5 *(1+Sin (:X + 220))]  # not 240!
end

to rgb2
  # this is library item 'Hue2'
  Make "Red [Abs 255 *Sin :X]
  Make "Gre [Abs 255 *Sin (:X + 120)]
  Make "Blu [Abs 255 *Sin (:X + 240)]
end

to rgb3
  Make "Red [127 *((Sin :X) + Abs Sin :X)]
  Make "Gre [127 *((Sin :X + 120) + Abs Sin :X + 120)]
  Make "Blu [127 *((Sin :X + 240) + Abs Sin :X + 240)]
end

to rgb4
  Make "Red [Abs 255 *Sin (:X/2)]
  Make "Gre [Abs 255 *Sin (:X/2 + 120)]
  Make "Blu [Abs 255 *Sin (:X/2 + 240)]
end
