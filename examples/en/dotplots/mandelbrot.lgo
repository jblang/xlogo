# Main Command: Go 4
# Mandelbrot_Set by Guy Walker
# www.cr31.co.uk/logoarts

to new
  # set default screen, pen and turtle 
  ResetAll SetScreenSize [400 400] HideTurtle 
  SetSC Black SetPC Green SetPS 1 PenUp 
end

to mand :mp :np
  Make "M 0 Make "N 0 Make "Count 0
  Repeat 90 [
    Make "Mnew (Power :M 2) - (Power :N 2) + :Mp
    Make "N (2*:M*:N) + :Np
    Make "M :Mnew
    Make "Count :Count + 1
    If ((Power :M 2) + (Power :N 2)) > 4 [
      SetPC PenCol :Count Stop] ]
end

to pencol :theta
  Make "Gre 255 *Sin :Theta
  Output ( List 0 :Gre 0 )
end

to go :order
  New
  Make "Size Item :Order [24 16 12 8 6 4 3 2 1]
  SetPW :Size
  Make "Start (Integer :Size/2)-192
  For (List "Y 0 191 :Size) [
    For (List "X :Start 191 :Size) [
    SetPC Black Mand (:X/140)-0.7 :Y/140
    SetXY :X :Y Dot Pos
    SetXY :X Minus :Y Dot Pos ] ]
end
